#!/bin/zsh

# See `man zshcompsys` for more information.

autoload -U compinit && compinit

# Show hidden files too.
_comp_options+=(globdots)

# Use builtin completion menu. (?)
zstyle ':completion:*' use-compctl true

# Show description for argument completions.
zstyle ':completion:*' verbose true

# List newly installed executables (without restarting Zsh).
zstyle ':completion:*' rehash true

# Set up types of completion.
zstyle ':completion:*' completer _complete _approximate _expand

# Case insensitive match.
zstyle ':completion:*' 'm:{a-zA-Z}={A-Za-z}' 'r:|[._-]=* r:|=*' 'l:|=* r:|=*'

# Ignore completion to commands we don't have.
zstyle ':completion:*:functions' ignored-patterns '_*'

# List directories first
zstyle ':completion:*' list-dirs-first true

# Highlight currently completed element (hit Tab more than once).
zstyle ':completion:*' menu select=1
zstyle ':completion:*' list-prompt %SAt %p: Hit TAB for more, or the character to insert%s
zstyle ':completion:*' select-prompt %SScrolling active: current selection at %p%s

# Print the group name of completions before the exact elements.
zstyle ':completion:*' format $'\n%F{yellow}== %d%f'

# Don't know exactly whats these doing, but I copied from Debian's .zshrc.
zstyle ':completion:*' auto-description 'specify: %d'

# All different types of matches displayed separately
zstyle ':completion:*' group-name ''

# Use the same colors for listing files as `ls`.
eval "$(dircolors -b)"
zstyle ':completion:*:default' list-colors ${(s.:.)LS_COLORS}

# Custom completion for `kill`.
zstyle ':completion:*:*:kill:*:processes' list-colors '=(#b) #([0-9]#)*=0=01;31'
zstyle ':completion:*:kill:*' command 'ps -u $USER -o pid,%cpu,tty,cputime,cmd'

# __ FZF
#
# https://github.com/junegunn/fzf
# https://github.com/lincheney/fzf-tab-completion

# Enable FZF tab-completion
zstyle ':completion:*' fzf-search-display true

# Preview `git diff` when completing git add
zstyle ':completion::*:git::git,add,*' fzf-completion-opts --preview='
for arg in {+1}; do
  { git diff --color=always -- "$arg" } 2>/dev/null
done'

# Compare with master when completing git checkout
zstyle ':completion::*:git::git,checkout,*' fzf-completion-opts --preview='
for arg in {+1}; do
  { git --no-pager log --oneline --graph --right-only --date-order --boundary --pretty="format:%s" master.."$arg" } 2>/dev/null
done'

[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh

export FZF_COMPLETION_AUTO_COMMON_PREFIX=true
export FZF_COMPLETION_AUTO_COMMON_PREFIX_PART=true
source ~/.config/zsh/bin/fzf-zsh-completion.sh

# __ PYTHON PIP COMPLETE

function _pip_completion
{
  local words cword
  read -Ac words
  read -cn cword
  reply=( $( COMP_WORDS="$words[*]" \
             COMP_CWORD=$(( cword-1 )) \
             PIP_AUTO_COMPLETE=1 $words[1] ) )
}
compctl -K _pip_completion pip
compctl -K _pip_completion pip2
compctl -K _pip_completion pip3

# __ KUBERNETES

(which kubectl > /dev/null) && source <(kubectl completion zsh)
